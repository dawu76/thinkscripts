# authors: TraderLion member @diamond

input kmap_percent = 2.0;
input price_offset = 3.0;
input enforce_upward_trend = yes;

def kmap_fraction = kmap_percent/100;
def price_offset_fraction = price_offset/100;

def sma10 = Average(close, 10);
def sma21 = Average(close, 21);
def sma50 = Average(close, 50);

def upward_trend;

if enforce_upward_trend {
    upward_trend = if sma50 > sma50[1] then yes else no;
} else {
    upward_trend = yes;
}

# Grab the lowest and the highest KMA
def max = max(max(sma10, sma21),sma50);
def min = min(min(sma10,sma21),sma50);

def high_kma = max;
def low_kma = min;
def pinch_percent;
def distance_to_kma;

# determine whether we will use high or low KMA to find the % distance to the price
if open < low_kma {
    distance_to_kma = absValue((low_kma/open)-1);
} else {
    distance_to_kma = absValue((high_kma/open)-1);
}

# get percent difference between high & low KMAs
pinch_percent = (high_kma/low_kma)-1;

# plot arrows for pinch spots in the past for review
plot kmap_lp = if pinch_percent < kmap_fraction and
                  distance_to_kma < price_offset_fraction and
                  upward_trend then yes
               else no;

# get KMAP percent as a label in order to see the spread between high and low
addlabel(1,
         "KMAP Spread: " + asPercent(pinch_percent),
         if pinch_percent < kmap_fraction then color.green
         else if pinch_percent < kmap_fraction+0.005 then color.gray
         else color.red
);

kmap_lp.SetDefaultColor(color.YELLOW);
kmap_lp.SetPaintingStrategy(PaintingStrategy.BOOLEAN_Arrow_Down);
kmap_lp.hideTitle();
