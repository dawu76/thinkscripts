# ATR Daily Range
# tomsk
# 12.17.2019

# V1.0 - 12.16.2019 - tomsk   - Initial release ATR Daily Range
# V1.1 - 12.16.2019 - tomsk   - Added performance relative to ATR range
# V1.2 - 12.17.2019 - tomsk   - Added plot lines for ATR High/Low on the chart
# V1.2 - 12.09.2021 - hwu     - Cosmetic labeling changes

# Displays ATR High/Low thresholds relative to daily open

input length = 14;
input averageType = AverageType.WILDERS;

def o = open(period = AggregationPeriod.DAY);
def h = high(period = AggregationPeriod.DAY);
def l = low(period = AggregationPeriod.DAY);
def c = close(period = AggregationPeriod.DAY);

def R = (c - l) / (h - l);
def ATRD = MovingAverage(averageType, TrueRange(h, c, l), length);
def ATRH = o + ATRD;
def ATRL = o - ATRD;

AddLabel(1, "Expected high/low based on ATR: " +
            "[ " + Round(ATRH,2) + " / " + Round(ATRL,2) + " ]",
            Color.PINK);

AddLabel(1, "High: " + Round(h,2) + 
            " low: " + Round(l,2) +
            " close: " + Round(c,2) +
            " [ " + AsPercent(R) + " ]",
            Color.GREEN);

def LBN = if isNaN(c[-1]) and !isNaN(c) then barnumber() else Double.NaN;

plot ATR_High = if barNumber() >= highestAll(LBN)
                then highestAll(if isNaN(c[-1]) then ATRH else Double.NaN)
                else Double.NaN;

plot ATR_Low = if barNumber() >= highestAll(LBN)
               then highestAll(if isNaN(c[-1]) then ATRL else Double.NaN) else Double.NaN;

ATR_High.SetLineWeight(2);
ATR_High.SetDefaultColor(Color.Cyan);

ATR_Low.SetLineWeight(2);
ATR_Low.SetDefaultColor(Color.Yellow);
